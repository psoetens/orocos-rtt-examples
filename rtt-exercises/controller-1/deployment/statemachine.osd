
/**
 * This statemachine file must be loaded in the ModeSwitch
 * component. See the last line for the name of the statemachine
 * instance we will create. See the Component Builder's Manual
 * for instructions on how to load and start state machines.
 */
StateMachine OurStateMachineType
{
	const int TIMER_ID = 1;
	var int received_id;
	var string new_mode;

    /*
     * Exercise: (do this one last)
     * INIT goes immediately to Automatic. Change this by
     * adding an intermediate state 'Waiting' which
     * waits for 5 seconds before making the transition
     * to Automatic. Use the Timer component for this. First choose
     * the timer number (id) you want to use. You must
     * 'arm' a timer and then wait for the 'timeout' event to happen.
     * When it happens (and it was our timer id that expired),
     * go to the Automatic state.
     */
	initial state INIT {
		entry {
			do Timer.arm(TIMER_ID, 5.0);
		}
		transition Timer.timeout(received_id)
			if (received_id == TIMER_ID) then select Automatic;
        //transition select Automatic;
    }

    state Automatic {
        entry {
            try Automatic.start();
            try Controller.start();
        }

        exit {
            try Automatic.stop();
            try Controller.stop();
        }

        /*
         * Exercise: Switch to the Manual Mode
         * when ModeSwitch signals this with a
         * switchMode event.
         */
        transition switchMode(new_mode)
			if (new_mode == "manual") then select Manual;
    }

    state Manual {
        entry {
            try Joystick.start();
        }

        /*
         * Exercise: write a 'run' program that checks
         * the joysticks output value. If the value is larger
         * than 10.0 or smaller than -10.0, switch the Joystick
         * to the run time 'warning' state, if the value is between
         * 10.0 and -10.0 again, 'recover' from the warning state.
         */
        run {
        	if (Joystick.output.Get() > 10.0 || Joystick.output.Get() < -10.0 ) then {
        		do Joystick.warning();
        	}
		/** See Joystick.hpp for the use of recovered() */
        }


        exit {
            try Joystick.stop();
        }

        /*
         * Exercise: Switch to the Automatic Mode
         * when ModeSwitch signals this with a
         * switchMode event.
         */
        transition switchMode(new_mode)
			if (new_mode == "automatic" && safetySwitch == true ) then
				select Automatic;

        /*
         * Exercise: go to automatic mode when
         * the joystick component is in an error state.
         */
        transition
			if Joystick.inRunTimeError() && safetySwitch == true then
				select Automatic;

        /*
         * Exercise: If the safety switch attribute of the ModeSwitch
         * component is false, do not go to the automatic mode.
         */
    }

    final state FINI {
    }
}

/*
 * This line 'instantiates' one statemachine of the
 * above declared type.
 */
RootMachine OurStateMachineType the_statemachine


